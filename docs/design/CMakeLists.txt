find_program (JAVA_PATH java)
if (JAVA_PATH)
    add_custom_target (documentation-design)
    add_dependencies (documentation documentation-design)

    # Processing PlantUML JAR
    set (PLANTUML_JAR "plantuml.jar")
    set (PLANTUML_FOUND "FALSE")
    if ("${PLANTUML_JAR_DIR}" STREQUAL "")
    else ()
        set (PLANTUML_JAR_PATH "${PLANTUML_JAR_DIR}/${PLANTUML_JAR}")
        if (EXISTS ${PLANTUML_JAR_PATH})
            set (PLANTUML_FOUND "TRUE")
        else ()
            message (FATAL_ERROR "Plantuml JAR file '${PLANTUML_JAR_PATH}' not found")
        endif ()
    endif ()
    if (NOT ${PLANTUML_FOUND})
        set (DOWNLOAD_PLANTUML_JAR_TARGET download-plantuml-jar)
        set (PLANTUML_JAR_PATH "${CMAKE_CURRENT_SOURCE_DIR}/${PLANTUML_JAR}")
        add_custom_command (OUTPUT ${PLANTUML_JAR_PATH}
            COMMAND ${CMAKE_COMMAND} -DPLANTUML_JAR_PATH=${PLANTUML_JAR_PATH} -P ${CMAKE_CURRENT_SOURCE_DIR}/download_plantuml.cmake)
        set_source_files_properties (${PLANTUML_JAR_PATH} PROPERTIES GENERATED 1)
        add_custom_target (${DOWNLOAD_PLANTUML_JAR_TARGET} SOURCES ${PLANTUML_JAR_PATH})
    endif ()

    add_subdirectory (libwatch)
else ()
    message (WARNING "Java is not found - design documentation is not available")
endif ()
